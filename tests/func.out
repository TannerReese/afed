# Test user-defined functions and lambda expressions

# Variable capture
scope: {
   t: 3.56,
   f x: x + t,
},
scope.f 4 = `7.5600000000000005`,

# Multiple arguments
{
    f f x1 x2 x3 x4 : f + x1 - x2 + x3 - x4,
    f 1 2 3 4 5 = `-1`,
},

# Currying & Lambda expressions
{
    g x y: x - y * x^2,
	f g: \r: g (g r),
	f (g (1/10)) 2 = `62 / 625`,
},

# Application operator
{
   if (2 + 2 > 4) 1 $ if (2 + 3 < 3.4) 0 $ (\x: x + x) $ 4 = `8`,
   f t: t^2, f $ f $ f 1.1 = `2.143588810000001`,
},

# Lambda expressions as arguments
{
	utils.with5 $ lamb 4.1 = `5.319512195121951`,
	utils: { with5 f: f 5 },
    lamb: \x: \y: x + y / x,
},

# Substitutions inside functions
{
	t: 4.56,
    sub x: (3 * t = `13.68`) + (x = `Eval Error: Depends on non-constant argument x`),
	sub 2 = `15.68`,
},

# Function unimplemented operations
{
	f x: \y: x + 1 / y,
	f + 3 = `Eval Error: Binary operator + not implemented between Func<name='f', id=11, arity=1> and 3`,
	4 * f = `Eval Error: Binary operator * not implemented between 4 and Func<name='f', id=11, arity=1>`,
	f 4 3 = `13 / 3`, # Works afterwards
},

# Method support
{
	math.sqrt.arity = `1`,
	atan2.arity = `2`,
	f.arity = `1`,
	g.arity = `1`,
	((\x y z: x + y + z) 3).arity = `2`,

	f x: [x, x],
	g: \y: [y, 2*y],
	f.hello = `Eval Error: Cannot call method hello on type function`,
	g.world = `Eval Error: Cannot call method world on type function`,
	f 2 = `[2, 2]`,
	g 1 = `[1, 2]`,
},

# Argument destructuring
{
	f [x, [_, _], {"_ a": x2}, y] : x - x2 / y,
	f [1, [0, 0], {"_ a": 3}, 2] = `-1 / 2`,

	# Fuzzy matching
	g {"df": x, .., h: [_, y]}: x - y,
	g {h: [1, 3], df: 2, other: "j"} = `-1`,

	# Lambdas
	(\{.., a: z, c: _, "b": [x, y]} : x + 2 * y + 4 * z)
	{a: 6, b: [1, 2], c: "a", d: null} = `29`,

	# Check erroneous calls
	g {df: 3, other: -1} = `Eval Error: Map is missing key h`,
	f [1, [2, 2], 3, 4] = `Eval Error: Expected map, but found number`,
	f [9, [1, 1], {"_ a": 4, n: 7}, 3] = `Eval Error: Map contains unused keys ["n"]`,
},

# Lambda expression with internal variables
{
	f x: {v: x * 2, w: x + 1, ans: v - w}.ans,
	f 4 = `3`,
	lamb x: \y: {v: y + x, w: y / 2, ans: v * w}.ans,
	lamb 1 5 = `15`,
}

# Duplicated arguments
{ f x x: x, f 3 4 = `Eval Error: Unresolved name "f"`},

